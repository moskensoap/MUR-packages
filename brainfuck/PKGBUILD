# Maintainer: moskensoap <141073988+moskensoap@users.noreply.github.com>

pkgname="mur__brainfuck"
pkgver=3.3
pkgrel=1
pkgdesc='Brainfuck interpreter and compiler written in C with automatic memory expansion. Brainfuck generator, Ook!, Blub, and Phantom translators written in Brainfuck.'
arch=('x86_64')
url='https://github.com/moskensoap/brainfuck'
license=('MIT' 'apache' 'CCBYSA')
makedepends=("make"
             "gcc")
depends=('gcc'
         'mur__optfirst')

source=("$url/archive/refs/tags/$pkgver.zip")
sha256sums=('6c09e98d0a60594901098047895b528c5c78175c9b78f932d0bbce753799683a')

prepare() {
  cd ${srcdir}
  cd brainfuck-$pkgver
  sed -i 's/char cc_command\[25 + strlen(argv\[i\])\];/char cc_command\[34 + strlen(argv\[i\])\];/g' brainfuck-compiler.c
  sed -i 's/cc -x c/usrfirst cc -x c/g' brainfuck-compiler.c
}

build() {
  cd ${srcdir}
  cd brainfuck-$pkgver
  make
  make others
  find examples-from-others -name '*.exe' -exec rm {} +
}

package() {
  cd ${srcdir}
  cd brainfuck-$pkgver

  mkdir -p ${pkgdir}/usr/local/bin/
  install -Dm755 *.exe ${pkgdir}/usr/local/bin/

  mkdir -p ${pkgdir}/usr/local/share/licenses/${pkgname}/
  install -Dm644 LICENSE* ${pkgdir}/usr/local/share/licenses/${pkgname}/
  
  mkdir -p ${pkgdir}/usr/local/share/$pkgname/
  cp -r examples-from-others/* ${pkgdir}/usr/local/share/$pkgname/

  cp -r my-bf-programs/* ${pkgdir}/usr/local/share/$pkgname/
}
