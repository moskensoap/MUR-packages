# Maintainer: moskensoap <141073988+moskensoap@users.noreply.github.com>

pkgname="mur__jdk8-openjdk"
pkgver=8.0.422
zuluver=8.80.0.17
jdkdir=zulu${zuluver}-ca-jdk${pkgver}-win_x64
pkgrel=1
pkgdesc='OpenJDK Java 8 development kit'
arch=('x86_64')
url='https://www.azul.com/downloads/#zulu'
license=(GPL2)
makedepends=("gcc"
             "make")
depends=("mur__jdk-openjdk")
source=("https://cdn.azul.com/zulu/bin/zulu${zuluver}-ca-jdk${pkgver}-win_x64.zip"
        "main.c"
        "makefile")
sha256sums=('f8976ecb98f64378b1aeea20d8014531ce956b50276d5c297438743f0065d7e8'
            '2e5391a82daf4e3dc5fdc8488036a9d0dc2b5cc30e1307484415c19e00e7ab86'
            '28f78fdc4f091b66334e4c8c4326ddaadb502d56aa57a29c5ad199af624ad199')

prepare() {
  cd ${srcdir}
  for file in $(ls $jdkdir/bin | grep .exe | sed 's/\.exe$/.c/'); do
    cp main.c $file
  done
  rm main.c
}

build() {
  cd ${srcdir}
  make
}

package() {
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/bin/
  install -Dm755 ./*.exe ${pkgdir}/opt/bin/
  mkdir -p ${pkgdir}/opt/share/java/${pkgname}/
  cp -a $jdkdir/* ${pkgdir}/opt/share/java/${pkgname}/
}