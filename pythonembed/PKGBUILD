# Maintainer: moskensoap <141073988+moskensoap@users.noreply.github.com>

pkgname="mur__pythonembed"
pkgver=3.12.5

major=${pkgver%%.*}
minor=${pkgver#*.}
minor=${minor%%.*}
vernum="${major}${minor}"  

pkgrel=2
pkgdesc='Python is a programming language that lets you work more quickly and integrate your systems more effectively.'
arch=('x86_64')
url='https://www.python.org'
license=('Python License')
makedepends=("gcc"
             "make")
depends=("gcc"
         "make"
         "mur__optfirst")

source=("$url/ftp/python/$pkgver/python-$pkgver-embed-amd64.zip"
        "https://github.com/python/cpython/archive/refs/tags/v$pkgver.zip"
        "link.c"
        "linkmakefile"
        "makefile"
        "pip_installed_exe_link.c"
        "pipmakefile"
        "pythonmain.c"
        "setversion-python.c")

noextract=("python-$pkgver-embed-amd64.zip"
           "v$pkgver.zip")

sha256sums=('54d4a758fbc87bd425a32b2f390077e6e8c2c0155d9907739240322ee84e44ab'
            'c6875be15e5ef8ebfc00282c09c624c5bba87fbd9ef0becaa39da6d0363b1ab9'
            'f5e835d8367cc65e9ffb46a626d357e8e8c7346a6f53d650917f3b40ef7aa963'
            '8ee348f88fcc54b406fd826951db824630929ecebcff535d4be8a6942a5f8238'
            '97c83b595a88a6668b3bfd6335162b8850be57b51cb9317819337369dff6733a'
            '57e4435c4568645029dfc03b01bf65ae86807c328ef7c6fefdc101d59222dff0'
            '67d52af4f94a74068745123f5a7d70513129496a162b971928de7ba8b629bd71'
            '73ceeb288a4a2f797ac2cf2fb75c8f3537f327a8a6ca96db9c7c35dee3604f14'
            'e794911c4b66c6292ededb7f5082b08ad64fa6821ba6ecee31bbe13f8d58ef34')

prepare(){
  cd ${srcdir}
  unzip v$pkgver.zip
  unzip python-$pkgver-embed-amd64.zip -d python-$pkgver-embed-amd64
}

build(){
  cd ${srcdir}
  cd python-$pkgver-embed-amd64

  #Uncomment to run site.main() automatically
  sed -i '5s/^#import/import/' python$vernum._pth

  #Copy ensurepip from 'github.com/python/cpython' to enable pip installation by python.exe or virtualenv.exe
  cp -a ../cpython-$pkgver/Lib/ensurepip/ .


  #prepare rename .c files to build python.exe and pip.exe wrapper
  cd ..
  mkdir wrapper
  for file in $(ls python-$pkgver-embed-amd64 | grep .exe | sed 's/\.exe$/.c/'); do
    cp pythonmain.c ./wrapper/$file
  done

  cp makefile ./wrapper/makefile

  #make python.exe and pip.exe wrapper
  cd wrapper
  make

  #prepare rename .c files to build python.exe and pip.exe linker
  cd ..
  mkdir linker

  for file in $(ls python-$pkgver-embed-amd64 | grep .exe | sed 's/\.exe$/.c/'); do
    cp link.c ./linker/$file
  done


  cp linkmakefile ./linker/makefile

  #make python.exe and pip.exe linker
  cd linker
  make

  #build setversion-python
  cd ..
  gcc -o setversion-python.exe setversion-python.c
}


package(){
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/bin/
  mkdir -p ${pkgdir}/opt/share/python/${pkgname}/
  cp -a python-$pkgver-embed-amd64/* ${pkgdir}/opt/share/python/${pkgname}/
  install -Dm755 wrapper/*.exe ${pkgdir}/opt/bin/
  install -Dm755 linker/*.exe ${pkgdir}/opt/bin/
  install -Dm755 setversion-python.exe ${pkgdir}/opt/bin/

  cp pip_installed_exe_link.c ${pkgdir}/opt/share/python/
  cp pipmakefile ${pkgdir}/opt/share/python/linkmakefile

}

options=(!strip)