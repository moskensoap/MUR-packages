# Maintainer: moskensoap <141073988+moskensoap@users.noreply.github.com>

pkgname="mur__pythonembed"
pkgver=3.12.4

major=${pkgver%%.*}
minor=${pkgver#*.}
minor=${minor%%.*}
vernum="${major}${minor}"  

pkgrel=1
pkgdesc='Python is a programming language that lets you work more quickly and integrate your systems more effectively.'
arch=('any')
url='https://www.python.org'
license=(GPL-compatible)
makedepends=("gcc"
             "make")
depends=("gcc"
         "make"
         "mur__optfirst")

source=("$url/ftp/python/$pkgver/python-$pkgver-embed-amd64.zip"
        "https://github.com/python/cpython/archive/refs/tags/v$pkgver.zip"
        "link.c"
        "linkmakefile"
        "makefile"
        "pip_installed_exe_link.c"
        "pipmakefile"
        "pythonmain.c"
        "setversion-python.c")

noextract=("python-$pkgver-embed-amd64.zip"
           "v$pkgver.zip")

sha256sums=('15fea3c9367653a85086fe37216b4d1a1c78688fa5e1587e1db0b0f658856564'
            '73ef3228caff56d350602977ed841c7e635d5ef0a47c98d35e529922d98815dc'
            'ca882fc7a6ae2dc402e8da472abb5c048f32f032eee849501955b052441e45e4'
            '490009190e4fdb5f11e5c716966fc26c96fcb69eb23e594c123ba28f03e4b4a6'
            'e45cdbda7a078597075fb0a9e1dbe3762a3313c73cbc81d34024a61320a81a50'
            '9f50bb7a4396c011245cde06cd4f61cb53a15b3ded38d685d1ca3cc9086da75b'
            '5559bd60035a8941c3e69e295f30eb52474d4f3342b8926dea621bd0a1106171'
            '81a959b173f5d21df4284994b2037c22df4fbe59e13c47cd718c827b39fba3fa'
            '7337362f982e1f8a96ab4aca155c06d755066eb25e81ca2249ed99f18762d266')

prepare(){
  cd ${srcdir}
  unzip v$pkgver.zip
  unzip python-$pkgver-embed-amd64.zip -d python-$pkgver-embed-amd64
}

build(){
  cd ${srcdir}
  cd python-$pkgver-embed-amd64

  #Uncomment to run site.main() automatically
  sed -i '5s/^#import/import/' python$vernum._pth

  #Copy ensurepip from 'github.com/python/cpython' to enable pip installation by python.exe or virtualenv.exe
  cp -a ../cpython-$pkgver/Lib/ensurepip/ .


  #prepare rename .c files to build python.exe and pip.exe wrapper
  cd ..
  mkdir wrapper
  for file in $(ls python-$pkgver-embed-amd64 | grep .exe | sed 's/\.exe$/.c/'); do
    cp pythonmain.c ./wrapper/$file
  done

  cp makefile ./wrapper/makefile

  #make python.exe and pip.exe wrapper
  cd wrapper
  make

  #prepare rename .c files to build python.exe and pip.exe linker
  cd ..
  mkdir linker

  for file in $(ls python-$pkgver-embed-amd64 | grep .exe | sed 's/\.exe$/.c/'); do
    cp link.c ./linker/$file
  done


  cp linkmakefile ./linker/makefile

  #make python.exe and pip.exe linker
  cd linker
  make

  #build setversion-python
  cd ..
  gcc -o setversion-python.exe setversion-python.c
}


package(){
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/bin/
  mkdir -p ${pkgdir}/opt/share/python/${pkgname}/
  cp -a python-$pkgver-embed-amd64/* ${pkgdir}/opt/share/python/${pkgname}/
  install -Dm755 wrapper/*.exe ${pkgdir}/opt/bin/
  install -Dm755 linker/*.exe ${pkgdir}/opt/bin/
  install -Dm755 setversion-python.exe ${pkgdir}/opt/bin/

  cp pip_installed_exe_link.c ${pkgdir}/opt/share/python/
  cp pipmakefile ${pkgdir}/opt/share/python/linkmakefile

}

options=(!strip)