# Maintainer: moskensoap <141073988+moskensoap@users.noreply.github.com>

pkgname="mur__pythonembed311"
pkgver=3.11.9

major=${pkgver%%.*}
minor=${pkgver#*.}
minor=${minor%%.*}
vernum="${major}${minor}"  

pkgrel=2
pkgdesc='Python311 is a programming language that lets you work more quickly and integrate your systems more effectively.'
arch=('x86_64')
url='https://www.python.org'
license=('Python License')
makedepends=("gcc"
             "make")
depends=("gcc"
         "make"
         "mur__optfirst"
         "mur__pythonembed")

source=("$url/ftp/python/$pkgver/python-$pkgver-embed-amd64.zip"
        "https://github.com/python/cpython/archive/refs/tags/v$pkgver.zip"
        "makefile"
        "pythonmain.c")

noextract=("python-$pkgver-embed-amd64.zip"
           "v$pkgver.zip")

sha256sums=('009d6bf7e3b2ddca3d784fa09f90fe54336d5b60f0e0f305c37f400bf83cfd3b'
            '5b246ed63c6a915e82e3089e32bb7d4ef7547ecc5cfe003dc17501aa91fb5676'
            'c7a0217ab48072e7e1f4101aeae7deab4f4eb4251eec89ea4dd83553a320dc64'
            '11ef9f0d6ddee8b1cac2cc3067481f757f6b18a7c2cbaaa7a5311e5b164e0989')

prepare(){
  cd ${srcdir}
  unzip v$pkgver.zip
  unzip python-$pkgver-embed-amd64.zip -d python-$pkgver-embed-amd64
}

build(){
  cd ${srcdir}
  cd python-$pkgver-embed-amd64

  #Uncomment to run site.main() automatically
  sed -i '5s/^#import/import/' python$vernum._pth

  #Copy ensurepip from 'github.com/python/cpython' to enable pip installation by python.exe or virtualenv.exe
  cp -a ../cpython-$pkgver/Lib/ensurepip/ .


  #prepare rename .c files to build python.exe and pip.exe wrapper
  cd ..
  mkdir wrapper
  for file in $(ls python-$pkgver-embed-amd64 | grep .exe | sed 's/\.exe$/.c/'); do
    cp pythonmain.c ./wrapper/$file
  done

  cp makefile ./wrapper/makefile

  #make python.exe and pip.exe wrapper
  cd wrapper
  make

}


package(){
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/bin/
  mkdir -p ${pkgdir}/opt/share/python/${pkgname}/
  cp -a python-$pkgver-embed-amd64/* ${pkgdir}/opt/share/python/${pkgname}/
  install -Dm755 wrapper/*.exe ${pkgdir}/opt/bin/

}

options=(!strip)